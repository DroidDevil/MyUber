import java.nio.charset.Charset

apply plugin: 'com.android.application'

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

// Uber Server Token
def uberServerToken
def uberServerTokenFile = file("uber_server_token")
if (uberServerTokenFile.exists()) {
    uberServerToken = GFileUtils.readFile(uberServerTokenFile)
} else {
    uberServerToken = "00000"
}

// Uber Client ID
def uberClientId
def uberClientIdFile = file("uber_client_id")
if (uberClientIdFile.exists()) {
    uberClientId = GFileUtils.readFile(uberClientIdFile)
} else {
    uberClientId = "00000"
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.droiddevil.myuber"
        minSdkVersion 15
        targetSdkVersion 21

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "UBER_SERVER_TOKEN", "\"${uberServerToken}\""
        buildConfigField "String", "UBER_CLIENT_ID", "\"${uberClientId}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Architecture
    compile 'com.netflix.rxjava:rxjava-core:0.20.6'
    compile 'com.netflix.rxjava:rxjava-android:0.20.6'

    // Services
    compile 'com.google.android.gms:play-services-base:6.5.+'
    compile 'com.google.android.gms:play-services-location:6.5.+'

    // DI
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    // Networking
    compile 'com.squareup.retrofit:retrofit:1.7.1'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'

    // Data
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.github.satyan:sugar:1.3.1'

    // Utils
    compile ('io.nlopez.smartlocation:library:3.0.2'){
        transitive = false;
    }

    // UI Utils
    compile 'com.squareup.picasso:picasso:2.3.4'
    compile 'com.jakewharton:butterknife:5.1.2'

    // UI
    compile 'de.hdodenhof:circleimageview:1.2.1'

    // Logging
    compile 'com.jakewharton.timber:timber:2.4.1'
}
